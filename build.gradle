plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'checkstyle'
}

group = 'com.sourcery'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

checkstyle {
    toolVersion = '10.3'
}

checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    postgresqlVersion = '42.4.3'
    liquibaseCoreVersion = '4.17.2'
    liquibasePropsFile = 'src/main/resources/db.properties'
    mybatisVersion = '3.0.1'
}

def props = new Properties()
file(project.ext.liquibasePropsFile).withInputStream { props.load(it) }

liquibase {
    activities {
        main {
            changeLogFile props["datasource.change-log"]
            url props["datasource.url"]
            username props["datasource.username"]
            password props["datasource.password"]
            defaultSchemaName props["datasource.schema"]
            classpath "$rootDir/src/main/resources"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework:spring-context"

    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    implementation "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.4'
    implementation 'org.springframework.security:spring-security-crypto:6.0.2'

    implementation 'org.springframework:spring-websocket:6.0.9'
    implementation 'org.springframework:spring-messaging:6.0.9'

    implementation 'org.springframework.boot:spring-boot-starter-security:3.0.4'
    implementation 'com.auth0:java-jwt:4.3.0'
    implementation 'com.auth0:jwks-rsa:0.22.0'

    liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseCoreVersion}"
    liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:2.1.1"
    liquibaseRuntime "org.postgresql:postgresql:${postgresqlVersion}"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'org.springframework:spring-test:6.0.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

